---
name: cangjie-references/cj-unittest_mock_package_interfaces
title: 接口
uri: https://developer.huawei.com/consumer/cn/doc/cangjie-references/cj-unittest_mock_package_interfaces
nodePath: 应用框架 / Cangjie（仓颉） / 仓颉标准库API / std.unittest.mock / 接口
---

# 接口

#### interface ValueListener<T>
    
    
    public interface ValueListener<T> {
        func lastValue(): Option<T>
        func allValues(): Array<T>
        func addCallback(callback: (T) -> Unit): Unit
        static func new(): ValueListener<T>
        static func onEach(callback: (T) -> Unit): ValueListener<T>
    }

功能：此接口提供了多个成员函数以支持“监听”传入给桩签名的参数。即对每次调用中传入桩签名的参数进行指定的操作（ addCallback() 或 onEach 中的闭包函数即为对参数进行的操作内容）。

一般与参数匹配器生成函数 argThat 或者 capture 配合使用。

值监听器与参数捕获器一同作为参数匹配器的一种被传入到桩签名中，作为入参。在定义桩签名的参数值范围的同时检查参数值。

举例来说：
    
    
    struct Animal {
        Animal(
            let name: String,
            let info: String
        ) {}
    }
    
    abstract class AnimalUi {
        func showAnimal(animal: Animal): Unit
    }
    
    let animals = [Animal("Smokey", "Cat, age: 5"), Animal("Daisy", "Dog, age: 9")]
    
    @Test
    func testAnimal(): Unit {
        let ui = mock<AnimalUi>()
        // 定义了一个值监听器：检查每个值，当值不满足条件时抛出异常。
        let listener = ValueListener<Animal>.onEach { animal =>
            if (animal.name == "Smokey") {
                @Assert(animal.info.contains("Cat"))
            }
        }
        // 定义一个桩签名的“桩行为”，参数匹配器为可执行上述值监听器的参数捕获器。
        @On(ui.showAnimal(capture(listener))).returns(())
    
        for (animal in animals) {
            // 此处将捕获传入的 animal 对象，并对其执行值监听器中的检查行为。
            ui.showAnimal(animal)
        }
    }

示例：
    
    
    import std.unittest.mock.*
    import std.unittest.mock.mockmacro.*
    
    class Processor {
        func process(name: String): String {
            return name.toAsciiTitle()
        }
    }
    
    @Test
    func test() {
        let originalProcessor = Processor()
        let processor = spy(originalProcessor)
        let captor = ValueListener<String>.new()
        @On(processor.process(capture(captor))).callsOriginal()
        processor.process("hello")
    
        let argumentValues = captor.allValues()
        @Assert(argumentValues.size == 1 && argumentValues[0] == "hello")
        @Assert(captor.lastValue(), "hello")
    }

#### [h2]func addCallback((T) -> Unit)
    
    
    func addCallback(callback: (T) -> Unit): Unit

功能：为当前“值监听器”对象增加闭包函数，该函数将处理传入的参数值。

参数：

  * callback: (T) ->[Unit](https://developer.huawei.com/consumer/cn/doc/cangjie-references/cj-core_package_intrinsics#unit) \- 处理参数值的闭包函数。



#### [h2]func allValues()
    
    
    func allValues(): Array<T>

功能：返回当前“值监听器”对象已处理的所有值。

返回值：

  * [Array](https://developer.huawei.com/consumer/cn/doc/cangjie-references/cj-core_package_structs#struct-arrayt)<T> \- 返回“值监听器”对象所处理的所有值列表。



#### [h2]func lastValue()
    
    
    func lastValue(): Option<T>

功能：返回当前“值监听器”对象所处理的最后一个值。

返回值：

  * [Option](https://developer.huawei.com/consumer/cn/doc/cangjie-references/cj-core_package_enums#enum-optiont)<T> \- 返回“值监听器”对象所处理的最后一个值，不存在时，返回 None 。



#### [h2]func new()
    
    
    static func new(): ValueListener<T>

功能：创建一个新的“值监听器”对象，不包含任何处理参数的闭包方法。

返回值：

  * [ValueListener](https://developer.huawei.com/consumer/cn/doc/cangjie-references/cj-unittest_mock_package_interfaces#interface-valuelistenert)<T> \- “值监听器”对象。



#### [h2]func onEach((T) -> Unit)
    
    
    static func onEach(callback: (T) -> Unit): ValueListener<T>

功能：创建一个新的“值监听器”对象，带有一个处理参数的闭包方法。

参数：

  * callback: (T) ->[Unit](https://developer.huawei.com/consumer/cn/doc/cangjie-references/cj-core_package_intrinsics#unit) \- 处理参数值的闭包函数。



返回值：

  * [ValueListener](https://developer.huawei.com/consumer/cn/doc/cangjie-references/cj-unittest_mock_package_interfaces#interface-valuelistenert)<T> \- “值监听器”对象。


